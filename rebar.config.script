TMPDIR = os:getenv("TMPDIR", "/tmp"),
Compile = fun(Name0, Prog) ->
    Name = filename:join(TMPDIR, Name0),
    ok = file:write_file(Name, Prog, [write, exclusive]),
    Cmd = erlang:open_port({spawn, ["${CC-cc} -o /dev/null ", Name]},
            [stream, exit_status]),
    Status = receive
        {Cmd, {exit_status, 0}} ->
            true;
        {Cmd, {exit_status, _}} ->
            false
    end,
    ok = file:delete(Name),
    Status
end,
Only = fun(OS, Name, Prog, Supported, Unsupported) ->
    case os:type() of
        OS ->
            case Compile(Name, Prog) of
                true ->
                    Supported;
                false ->
                    Unsupported
            end;
        _ ->
            Unsupported
    end
end,
Linux = fun(Name, Prog, Supported, Unsupported) ->
    Only({unix,linux}, Name, Prog, Supported, Unsupported)
end,
Append = fun(Str, Flag) ->
    string:join(sets:to_list(sets:add_element(Flag,
                    sets:from_list(string:tokens(Str, " ")))), " ")
end,
Setns = fun(CONFIG) ->
    Prog = "
#define _GNU_SOURCE
#include <sched.h>
int main(int argc, char *argv[]) {
    (void)setns(0,0);
    return 0;
}",
    Flag = Linux("test_setns.c", Prog, "-DHAVE_SETNS", ""),
    Define = os:getenv("ALCOVE_DEFINE", ""),
    true = os:putenv("ALCOVE_DEFINE", Append(Define, Flag)),
    CONFIG
end,
Seccomp = fun(CONFIG) ->
    Prog = "
#include <linux/seccomp.h>
int main(int argc, char *argv[]) {
#ifdef SECCOMP_MODE_FILTER
    return 0;
#else
    return 1;
#endif
}",
    Flag = Linux("test_seccomp.c", Prog, "-DHAVE_SECCOMP", ""),
    Define = os:getenv("ALCOVE_DEFINE", ""),
    true = os:putenv("ALCOVE_DEFINE", Append(Define, Flag)),
    CONFIG
end,
lists:foldl(fun(Fun, Cfg) ->
        Fun(Cfg)
    end,
    CONFIG,
    [Setns, Seccomp]
).
